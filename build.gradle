plugins {
    id 'base'
    id 'maven-publish'
    id 'de.undercouch.download' version '5.6.0'
    id 'com.palantir.git-version' version '4.0.0'
}

ext.versionDetails = versionDetails()
version = versionDetails.lastTag

def downloadGameControllerTask = tasks.register("downloadGameController")
tasks["build"].configure {
    dependsOn(downloadGameControllerTask)
}

String gameControllerBinaryLocation = "$buildDir/binaries"

def binaries = [
        ["classifier": "windows_amd64", ext: ".exe"],
        // ["classifier": "windows_arm64", ext: ".exe"],
        ["classifier": "linux_amd64", ext: ""],
        // ["classifier": "linux_arm64", ext: ""],
        // ["classifier": "darwin_amd64", ext: ""],
        // ["classifier": "darwin_arm64", ext: ""],
]

binaries.each { binary ->

    // renovate: datasource=github-releases depName=RoboCup-SSL/ssl-game-controller
    def gameControllerVersion = "v0.0.1-beta"
    def gameControllerBinary = "ssl-game-controller_${gameControllerVersion}_${binary.classifier}${binary.ext}".toString()

    def task = tasks.register("downloadGameController_${binary.classifier}", Download) {
        src "https://github.com/lkhcode/ssl-game-controller/releases/download/${gameControllerVersion}/${gameControllerBinary}"
        dest file("${gameControllerBinaryLocation}/ssl-game-controller_${binary.classifier}${binary.ext}")
        overwrite true
    }

    tasks.register("gameController_${binary.classifier}", Jar) {
        archiveClassifier.set(binary.classifier)
        from(file("${gameControllerBinaryLocation}/ssl-game-controller_${binary.classifier}${binary.ext}")) {
            rename { "ssl-game-controller" }
        }
        dependsOn tasks["downloadGameController_${binary.classifier}"]
    }
    tasks["downloadGameController"].dependsOn(task)
}


tasks.register("gameController", Jar) {
    binaries.forEach { binary ->
        from(file("${gameControllerBinaryLocation}/ssl-game-controller_${binary.classifier}${binary.ext}")) {
            rename { "ssl-game-controller_${binary.classifier}" }
        }
    }
    dependsOn tasks["downloadGameController"]
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/lkhcode/ssl-game-controller-maven")
            credentials {
                // Prefer project properties for local overrides, fall back to environment variables (useful in CI)
                username = project.findProperty("githubPackagesUser") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("githubPackagesToken") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = 'com.github.lkhcode'
            artifactId = 'ssl-game-controller-maven'

            binaries.each { binary ->
                artifact(tasks["gameController_${binary.classifier}"]) {
                    classifier = binary.classifier
                }
            }
            artifact(tasks["gameController"])
        }
    }
}
